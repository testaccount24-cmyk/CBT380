ISREDIT MACRO
        ISREDIT (MEMBERX) = MEMBER
        ISREDIT (DSNX)    = DATASET
 /*===== SYSTECH PERSONNEL CHANGED FORM 0267 TO 0289 ONLY  ========*/
 /* PURPOSE OF X0289S AND X0289 CLISTS -- SYSTECH CHANGED          */
 /*   CHANGE THE FIRST CARD COST CENTER FROM 0267 TO 0289          */
 /*   CHANGE THE AREA CODE TO 908 ON '(201) 555-' CARDS            */
 /* DIRECTIONS TO USE THE X0289S CLIST AND IT'S COMPANION X0289    */
 /* 1. GET INTO EDIT ON MEMBER ZZ IN THE LIBRARY TO BE CHANGED     */
 /* 2. ON THE COMMAND LINE ENTER   ===> X0289S                     */
 /* 3. CHECK THE SRCHFOR PANEL THAT YOU HAVE                       */
 /*    A. THE CORRECT DSNAME INCLUDING AN (*) FOR MEMBERNAME       */
 /*    B. THAT YOU ARE SEARCHING ON STRINGS                        */
 /*       "0267,"   AND "(201) 555-"                               */
 /* 4. HIT ENTER TO INVOKE THE SEARCH                              */
 /* 5. HIT PFK-3 TO GET OUT OF BROWSE.                             */
 /* 6. CHANGES WILL COMMENCE, BY INVOKING 2ND CLIST                */
 /*      ISREDIT X0289                                             */
 /* 7. IF THE CLIST STOPS BEFORE COMPLETION ON AN SB37 OR          */
 /*    SOMETHING SIMILAR.  COMPRESS YOUR LIBRARY WITHOUT           */
 /*    LEAVING EDIT.  THEN HIT PFK-3 TO EXIT AND CONTINUE.         */
 /* 8. CHECK NUMBER OF MEMBERS PROCESSED AGAINST NUMBER OF         */
 /*    MEMBERS IN THE ZZ MEMBER.                                   */
 /*================================================================*/
ISREDIT F FIRST '='P
IF &LASTCC = 0 THEN DO
   SET &ZEDSMSG = &STR(NOT EMPTY)
   SET &ZEDLMSG = &STR(&ZEDSMSG. IS A REQUIREMENT, SHOULD BE MEMBER ZZ)
   ISREDIT LINE_BEFORE .ZF   =  MSGLINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISREDIT LINE_BEFORE .ZCSR = NOTELINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISPEXEC     SETMSG MSG(ISRZ000)
   EXIT CODE(1)
END
SET RP = &STR())
SET LP = &STR())
SET &ZSSFS1 = &STR('0267,')
SET &ZSSFS2 = &STR('(201) 555-')
SET &ZSSFS3 = &STR(   )
SET &ZSSFNDSN = &STR('&DSNX.(*&RP.'
SET &ZSSFMULT = &STR(YES)
ISPEXEC VPUT (ZSSFS1,ZSSFS2,ZSSFS3,ZSSFNDSN,ZSSFMULT) SHARED
ISPEXEC SELECT PGM(ISRSFM)
COPY SRCHFOR.LIST '&DSNX.(ZZX0289)' NONUM
ISREDIT COPY ZZX0289 AFTER 0
ISREDIT KILL ZZX0289
ISREDIT X 'ZZX0289' ALL
IF &STR(&SYSUID..ISPF.PROFILE) = &STR(&DSNX.) THEN DO
   /* CAN NOT DO YOUR OWN PROFILE  */
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISPPROF '
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISPSPROF '
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISRPROF '
END
ISREDIT DEL  X ALL
ISREDIT X ALL
ISREDIT F '-----' ALL
IF &LASTCC. ^= 0 THEN DO
   SET &ZEDSMSG = &STR(NO STRINGS FOUND)
   SET &ZEDLMSG = &STR(&ZEDSMSG. -- TERMINATING CLIST)
   ISREDIT LINE_BEFORE .ZCSR = NOTELINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISPEXEC     SETMSG MSG(ISRZ000)
   EXIT CODE(1)
END
SET I = &SYSINDEX(&STR(ISPF.PROFILE),&STR(&DSNX.            ))
IF &I = 0 THEN  ISREDIT @DIR STET $NDU IMAC MACRO(X0289) FIND(2)
          ELSE  ISREDIT @DIR SUPPRESS IMAC MACRO(X0289P) FIND(2)
SET &ZSSFNDSN = &STR()
ISPEXEC VPUT (ZSSFNDSN) SHARED
