/* REXX */
ADDRESS ISREDIT
"ISREDIT MACRO (HELP) NOPROCESS"
TRACE
IF help = '?' |,
   translate(help) = 'HELP' THEN
  DO
    ADDRESS ISPEXEC "DISPLAY PANEL(#EOL)"
    EXIT 1
  END
"ISREDIT (CL,CC) = CURSOR"
save_cc = cc
CALL find_end
/* If already at EOL, then jump to next line and go to EOL */
IF save_cc = cc THEN
  DO
    cl = cl + 1
    CALL find_end
  END
"ISREDIT CURSOR = " cl cc
EXIT
find_end:
"ISREDIT (WIDTH) = DATA_WIDTH"
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
"ISREDIT (xstatus) = xstatus" cl
if rc = 0 then
   if xstatus = "X" then do
        /* Bypass eXcluded lines */
        "ISREDIT FIND p'=' 1 NX"
        if rc > 0 then do   /* rc=4 from FIND */
           ZEDSMSG = '*BOTTOM OF DATA REACHED*'
           ZEDLMSG = "EOL found no more NX lines to process"
           ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
           EXIT
        END
        "ISREDIT (cl) = cursor"
   end
   else nop
else do    /* rc=12 from XSTATUS */
    "ISREDIT CURSOR = " cl-1 cc
    ZEDSMSG = '*BOTTOM OF DATA REACHED*'
    ZEDLMSG = 'No more lines to process'
    ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
    EXIT
END
"ISREDIT (LINE) = LINE" cl
DO cc = width to 1 by -1
  IF SUBSTR(line,cc,1) /= ' ' THEN
    LEAVE
END
IF cc = width THEN
  NOP
ELSE
  cc = cc + 1
RETURN
/* J. KALINICH, X4521 */
/* EDIT MACRO TO SET CURSOR AT END OF CURRENT SCREEN LINE          */
/* changes made to CBT match changes made/accepted by John later   */
/******************************************************************
 *      1994/06/28 15:56:40 TECH.CBT.FILE078.PDS(REXX)
 *      statistics    0105-93216-93216-1416-00042-00038-00000-REXX
 *          JOHN KALINICH
 *          USA SYSTEMS INTEGRATION &
 *           MANAGEMENT ACTIVITY
 *          AMXSI-TRI, ROOM 7.103
 *          1222 SPRUCE STREET
 *          ST. LOUIS, MO.  63103-2834
 *
 *          314-331-4521
 *          314-331-4520 (FAX)
 ******************************************************************
 The EOL macro is used to position the cursor at the end of the current screen
 line (like most PC editors do when you press the "End" scroll key).  Yes, it
 takes some interrupts and a PF key definition, but that is the price you have
 to pay for PC-like features on a 3270.  By pressing the EOL PF key repeatedly,
 you can jump to end of line (EOL) on successive lines.

 Use:
 Press appropriate PF key (the one assigned to EOL)

 COMMAND ===> <PFn>             Assume cursor was on word IN on line 000020
 Before ********************** TOP OF DATA *************************************
 000020 D:  IN A858SES FIND ALL RECORDS FOR WHICH
 000021       REC-TY = 'SESC'
 000022     END FIND                             .--- Cursor is now here
                                                 |
 After *********************** TOP OF DATA ***** v *****************************
 000020 D:  IN A858SES FIND ALL RECORDS FOR WHICH_
 000021       REC-TY = 'SESC'
 000022     END FIND
 ..........................*/
