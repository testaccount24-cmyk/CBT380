ISREDIT MACRO
/* CREATED FOR J.MULHEARN FROM X0289.. MEMBER 1991/01/24*/
        ISREDIT (MEMBERX) = MEMBER
        ISREDIT (DSNX)    = DATASET
 /*===== MULHEARN PERSONNEL CHANGED FORM 0289 TO 0282 ONLY  ========*/
 /* PURPOSE OF M0282S AND M0282 CLISTS -- MULHEARN CHANGED         */
 /*   CHANGE THE FIRST CARD COST CENTER FROM 0289 TO 0282          */
 /*   CHANGE THE AREA CODE TO 908 ON '(201) 555-' CARDS            */
 /* DIRECTIONS TO USE THE M0282S CLIST AND IT'S COMPANION M0282    */
 /* 1. GET INTO EDIT ON MEMBER ZZ IN THE LIBRARY TO BE CHANGED     */
 /* 2. ON THE COMMAND LINE ENTER   ===> M0282S                     */
 /* 3. CHECK THE SRCHFOR PANEL THAT YOU HAVE                       */
 /*    A. THE CORRECT DSNAME INCLUDING AN (*) FOR MEMBERNAME       */
 /*    B. THAT YOU ARE SEARCHING ON STRINGS                        */
 /*       "0289,"   AND "(201) 555-"                               */
 /* 4. HIT ENTER TO INVOKE THE SEARCH                              */
 /* 5. HIT PFK-3 TO GET OUT OF BROWSE.                             */
 /* 6. CHANGES WILL COMMENCE, BY INVOKING 2ND CLIST                */
 /*      ISREDIT M0282                                             */
 /* 7. IF THE CLIST STOPS BEFORE COMPLETION ON AN SB37 OR          */
 /*    SOMETHING SIMILAR.  COMPRESS YOUR LIBRARY WITHOUT           */
 /*    LEAVING EDIT.  THEN HIT PFK-3 TO EXIT AND CONTINUE.         */
 /* 8. CHECK NUMBER OF MEMBERS PROCESSED AGAINST NUMBER OF         */
 /*    MEMBERS IN THE ZZ MEMBER.                                   */
 /*================================================================*/
ISREDIT F FIRST '='P
IF &LASTCC = 0 THEN DO
   SET &ZEDSMSG = &STR(NOT EMPTY)
   SET &ZEDLMSG = &STR(&ZEDSMSG. IS A REQUIREMENT, SHOULD BE MEMBER ZZ)
   ISREDIT LINE_BEFORE .ZF   =  MSGLINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISREDIT LINE_BEFORE .ZCSR = NOTELINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISPEXEC     SETMSG MSG(ISRZ000)
   EXIT CODE(1)
END
SET RP = &STR())
SET LP = &STR())
SET &ZSSFS1 = &STR('0289,')
SET &ZSSFS2 = &STR('(201) 555-')
SET &ZSSFS3 = &STR(   )
SET &ZSSFNDSN = &STR('&DSNX.(*&RP.'
SET &ZSSFMULT = &STR(YES)
ISPEXEC VPUT (ZSSFS1,ZSSFS2,ZSSFS3,ZSSFNDSN,ZSSFMULT) SHARED
ISPEXEC SELECT PGM(ISRSFM)
COPY SRCHFOR.LIST '&DSNX.(ZZM0282)' NONUM
ISREDIT COPY ZZM0282 AFTER 0
ISREDIT KILL ZZM0282
ISREDIT X 'ZZM0282' ALL
IF &STR(&SYSUID..ISPF.PROFILE) = &STR(&DSNX.) THEN DO
   /* CAN NOT DO YOUR OWN PROFILE  */
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISPPROF '
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISPSPROF '
   ISREDIT CHANGE  ALL WORD ISRPROF  2 '******* ISRPROF '
END
ISREDIT DEL  X ALL
ISREDIT X ALL
ISREDIT F '-----' ALL
IF &LASTCC. ^= 0 THEN DO
   SET &ZEDSMSG = &STR(NO STRINGS FOUND)
   SET &ZEDLMSG = &STR(&ZEDSMSG. -- TERMINATING CLIST)
   ISREDIT LINE_BEFORE .ZCSR = NOTELINE "&SYSTIME. *ERROR** &ZEDLMSG."
   ISPEXEC     SETMSG MSG(ISRZ000)
   EXIT CODE(1)
END
SET I = &SYSINDEX(&STR(ISPF.PROFILE),&STR(&DSNX.            ))
IF &I = 0 THEN  ISREDIT @DIR STET $NDU IMAC MACRO(M0282) FIND(2)
          ELSE  ISREDIT @DIR SUPPRESS IMAC MACRO(M0282P) FIND(2)
SET &ZSSFNDSN = &STR()
ISPEXEC VPUT (ZSSFNDSN) SHARED
