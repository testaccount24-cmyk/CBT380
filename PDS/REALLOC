 PROC 1 DS FIRST LAST ADD REPLACE NOLIST LIST NOPROMPT PROMPT SHR OLD -
        HELP DELETE DDNAME(SYSPROC) DISP(SHR) DEBUG NOERROR
 CONTROL NOFLUSH
 IF &DS = HELP OR &HELP = HELP THEN DO
    HELP REALLOC
    EXIT CODE(0)
    END
 IF &OLD = OLD THEN SET DISP = OLD
 IF &PROMPT = PROMPT THEN DO
    SET LIST = LIST
    ISPEXEC CONTROL DISPLAY LINE
    END
 IF &LIST = LIST THEN CONTROL LIST NOFLUSH
 SET SYSOUTTRAP = 1000
 LISTA ST
 SET SYSOUTTRAP = 0
 SET FOUND = NO
 SET DSFOUND = NO
 SET CONCAT = &STR()
 SET PREF = &SYSPREF..
 SET PREFL = &LENGTH(&STR(&PREF))
 IF &SUBSTR(1:1,&DS) = ' THEN SET DS1 = &DS
 ELSE IF &SYSPREF = THEN SET DS1 = '&DS'
 ELSE SET DS1 = '&SYSPREF..&DS'
 SET I = 1
 DO WHILE &STR(&FOUND) = NO AND &SYSOUTLINE >= &I
    SET DDN = &&SYSOUTLINE&I..
    IF &DEBUG. = DEBUG THEN WRITE DEBUG DDN IS &DDN.
    IF &LENGTH(&STR(&DDN)) > 9 THEN -
       IF &STR(&SUBSTR(2:10,&DDN)) = &STR( &DDNAME) THEN DO
          SET FOUND = YES
          SET I = &I - 1
          SET DSN = &&SYSOUTLINE&I..
          IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
          IF &LIST = LIST THEN WRITE &DDNAME ALLOCATION IS
          IF &LIST = LIST THEN WRITE    '&DSN'
          SET DS2 = '&DSN'
          IF &LENGTH(&DSN) > &PREFL THEN -
             IF &SUBSTR(1:&PREFL,&DSN) = &STR(&PREF) THEN DO
                SET DSN = &SUBSTR(&EVAL(&PREFL+1):&LENGTH(&DSN),&DSN)
                IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
                END
             ELSE DO
                SET DSN = '&DSN'
                IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
                END
          ELSE DO
             SET DSN = '&DSN'
             IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
             END
          IF &REPLACE = AND &DS1 ^= &DS2 THEN SET CONCAT = &DSN
          IF &DS1 = &DS2 THEN SET DSFOUND = YES
          END
       ELSE SET I = &I + 1
    ELSE SET I = &I + 1
    END
 IF &FOUND = YES THEN DO
    DO WHILE &EVAL(&I+3) <= &SYSOUTLINE
       SET I = &I + 3
       SET DDN = &&SYSOUTLINE&I
       IF &DEBUG. = DEBUG THEN WRITE DEBUG DDN IS &DDN.
       IF &STR(&SUBSTR(1:3,&DDN)) = &STR() THEN DO
          IF &REPLACE = REPLACE AND &LAST = LAST THEN -
             SET CONCAT = &CONCAT&STR( ')&DSN'
          SET I = &I - 1
          SET DSN = &&SYSOUTLINE&I
          IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
          IF &LIST = LIST THEN WRITE    '&DSN'
          SET DS2 = '&DSN'
          IF &LENGTH(&DSN) > &PREFL THEN -
             IF &SUBSTR(1:&PREFL,&DSN) = &STR(&PREF) THEN DO
                SET DSN = &SUBSTR(&EVAL(&PREFL+1):&LENGTH(&DSN),&DSN)
                IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN.
                END
             ELSE DO
                SET DSN = '&DSN'
                IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN
                END
          ELSE DO
             SET DSN = '&DSN'
             IF &DEBUG. = DEBUG THEN WRITE DEBUG DSN IS &DSN
             END
          IF &REPLACE = OR &LAST = THEN -
             IF &DS1 ^= &DS2 THEN SET CONCAT = &CONCAT &DSN
          IF &DS1 = &DS2 THEN SET DSFOUND = YES
          END
       ELSE SET I = &SYSOUTLINE
       END
    END
 ELSE DO
    IF &LIST = LIST THEN WRITE &DDNAME IS NOT ALLOCATED.
    END
 IF &LIST = LIST THEN DO
    IF &DELETE = DELETE THEN DO
       IF &DSFOUND = YES THEN WRITE &DS WILL BE DELETED FROM THE LIST.
       END
    ELSE IF &REPLACE = REPLACE THEN DO
  IF &LAST = LAST THEN WRITE THE LAST DATASET WILL BE REPLACED BY &DS..
  ELSE WRITE THE FIRST DATASET WILL BE REPLACED BY &DS..
  END
    ELSE DO
  IF &LAST = LAST THEN WRITE &DS WILL BE ADDED TO THE END OF THIS LIST.
  ELSE WRITE &DS WILL BE ADDED TO THE TOP OF THIS LIST.
  END
    END
 IF &DELETE = DELETE THEN DO
    IF &DSFOUND = NO THEN DO
       IF &NOERROR ^= NOERROR THEN +
          WRITE DATASET &DS1 WAS NOT FOUND IN &DDNAME ALLOCATION.
       EXIT CODE(4)
       END
    SET CONCAT2 = &CONCAT
    END
 ELSE IF &LAST = LAST THEN SET CONCAT2 = &CONCAT &DS
 ELSE SET CONCAT2 = &DS &CONCAT
 IF &PROMPT = PROMPT THEN DO
    WRITE ALLOCATION WILL BE CHANGED WITH THE FOLLOWING COMMAND:
    IF &CONCAT2 = THEN WRITE    FREE FI(&DDNAME)
    ELSE WRITE    ALLOC FI(&DDNAME) DS(&CONCAT2) &DISP
    WRITENR ENTER YES TO EXECUTE THIS COMMAND ===>
    READ
    IF &SYSDVAL ^= YES THEN EXIT CODE(0)
    END
 IF &CONCAT2 = THEN FREE FI(&DDNAME)
 ELSE ALLOC FI(&DDNAME) DS(&CONCAT2) &DISP REUSE
 SET RC = &LASTCC
 IF &RC ^= 0 THEN DO
    WRITE ALLOCATION FAILED, RETURN CODE &RC..
    IF &PROMPT = PROMPT THEN DO
       WRITE ALLOCATION WILL BE CHANGED WITH THE FOLLOWING COMMAND:
       WRITE    ALLOC FI(&DDNAME) DS(&CONCAT) &DISP
       WRITENR ENTER YES TO EXECUTE THIS COMMAND ===>
       READ
       IF &SYSDVAL ^= YES THEN EXIT CODE(0)
       END
    ALLOC FI(&DDNAME) DS(&CONCAT) &DISP REUSE
    SET RC = &LASTCC
    END
 EXIT CODE(&RC)
