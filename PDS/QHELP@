 /*REXX  QHELP@ SIMILAR TO @STRAPQQ CONVERTED TO REXX FROM @STRAPQ*/
 "ISREDIT MACRO (COMMAND)              "
 /*  TITLE:       QHELP@ -- TRAP CLIST INVOKED BY QHELP CLIST       */
 /*  USERS:       TSO ISPF EDIT USERS                               */
 /*  -- QHELP@ IS USED EXCLUSIVELY BY QHELP                         */
 /*  ENTRY:       USED AS AN EDIT CLIST WITHIN ISPF EDIT            */
 /*        ===> QHELP ALLOCATE    (WILL INVOKE QHELP@ EXECUTE       */
 /*  CONTRIBUTED: 1990/12/17 DAVID MCRITCHIE                        */
 /*               PROFS ID AT BBBBBBBBBBB   MCRITCHI               */
 /*  EXAMPLE:  ===> QHELP@ DDNAME ALL                               */
 "ISREDIT CAPS OFF    "
 "ISREDIT BOUNDS      "
 "ISREDIT AUTOSAVE ON "
 "ISPEXEC VGET (QHELP) SHARED "
 IF LENGTH(COMMAND) = 0 THEN COMMAND = QHELP
 IF LENGTH(COMMAND) = 0 THEN DO
     SAY  ' PLEASE SUPPLY TSO COMMAND ENTRY FOR @STRAP MACRO CLIST'
     SAY  '   USE SINGLE QUOTES OR PARENTHESIS IF            '
     SAY  '          IF REPLY HAS SPACES OR COMMAS           '
     SAY  '   TO CANCEL REQUEST HIT ENTER                    '
     PULL COMMAND
     SAY "BY YOUR COMMAND ..." COMMAND
 END
 IF LENGTH(COMMAND) = 0 THEN DO
    SAY "REQUEST TERMINATED DUE TO NULL COMMAND ENTRY"
    EXIT 0
 END
 "ISREDIT CAPS OFF"
 COMMANDX = TRANSLATE(COMMAND)
 IF SUBSTR(COMMANDX,1,5) = "HELP " THEN COMMANDX = SUBSTR(COMMAND,6)
 "ISREDIT LINE_AFTER .ZLAST = '  '"
 "ISREDIT LINE_AFTER .ZLAST = 'COMMAND INVOKING QHELP@ EXECUTE,
   MACRO ===> QHELP "||COMMANDX||"'"
 "ISREDIT LINE_AFTER .ZLAST = DATALINE " ,
    "'EQUIVALENT TO TRAPPED OUTPUT ===> TSO "||COMMAND||"'"
 "ISREDIT  LINE_AFTER .ZLAST = '  '  "
 RESULT = OUTTRAP("SAVE.",'*')
 COMMAND
 RCMAX = RC
 RESULT = OUTTRAP("OFF")
 DO I = 1 TO SAVE.TRAPPED BY 1;/*IF I > 400 THEN LEAVE I--P.REMOVED*/
   XXXX   = TRANSLATE(SAVE.I,X2C('08'),'"')
   "ISREDIT LINE_AFTER .ZLAST = DATALINE """||XXXX||""""
 END
 "ISREDIT CHANGE ALL '"||X2C('08')||"' '""' "
 "ISREDIT LOC 0 "
 EXIT RCMAX
