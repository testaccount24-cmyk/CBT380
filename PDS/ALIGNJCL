CONTROL ASIS
ISREDIT MACRO NOPROCESS (BEGA,ENDB)
 WRITE In the future please use FIXJCL -- it works better.
/*  DESCRIPTION:   ALIGN JCL such that "EXEC" begin on col 12
/*                 and "DD" begins on col 13.
/*  PURPOSE:       Create more readable JCL.
/*  INSTALL:       into SYS1.TSOCLIST for use by everyone
/*  NOTE:          AUTOSAVE OFF   is not invoked -- no problems expected
/*                 default pair of labels is  .ZFIRST and .ZLAST
/*  Contributed: 1985/12/24 David McRitchie                       */
IF &BEGA. = THEN DO   /* set default labels if none supplied         */
   SET BEGA = &STR(.ZFIRST) /* default        ALIGNJCL .ZFIRST .ZLAST */
   SET ENDB = &STR(.ZLAST) /* you could use   ALIGNJCL .ZFIRST .ZLAST */
END
ISREDIT (MEMNAME) = MEMBER
ISREDIT (LBOUND,RBOUND) = BOUNDS
ISREDIT BOUNDS 1 71
ISREDIT reset &BEGA. &ENDB.
ISREDIT x all
ISREDIT find all 1 '//'
IF &LASTCC ^= 0 THEN EXIT CODE(0)
ISREDIT x all    1 '//*'
ISREDIT x all    12 'EXEC'
ISREDIT change &BEGA. &ENDB. all nx 12 ' DD   ' ' DD  '
ISREDIT x all    12 ' DD  '
ISREDIT change &BEGA. &ENDB. all nx  3 ' DD '    '          DD '
ISREDIT change &BEGA. &ENDB. all nx  4 ' DD '    '         DD '
ISREDIT change &BEGA. &ENDB. all nx  5 ' DD '    '        DD '
ISREDIT change &BEGA. &ENDB. all nx  6 ' DD '    '       DD '
ISREDIT change &BEGA. &ENDB. all nx  7 ' DD '    '      DD '
ISREDIT change &BEGA. &ENDB. all nx  8 ' DD '    '     DD '
ISREDIT change &BEGA. &ENDB. all nx  9 ' DD '    '    DD '
ISREDIT change &BEGA. &ENDB. all nx 10 ' DD '    '   DD '
ISREDIT change &BEGA. &ENDB. all nx 11 ' DD '    '  DD '
ISREDIT change &BEGA. &ENDB. all nx 11 '  DD  '  '  DD '
ISREDIT change &BEGA. &ENDB. all nx 11 '  DD '   '  DD  '
ISREDIT change &BEGA. &ENDB. all nx  3 ' EXEC '  '         EXEC '
ISREDIT change &BEGA. &ENDB. all nx  4 ' EXEC '  '        EXEC '
ISREDIT change &BEGA. &ENDB. all nx  5 ' EXEC '  '       EXEC '
ISREDIT change &BEGA. &ENDB. all nx  6 ' EXEC '  '      EXEC '
ISREDIT change &BEGA. &ENDB. all nx  7 ' EXEC '  '     EXEC '
ISREDIT change &BEGA. &ENDB. all nx  8 ' EXEC '  '    EXEC '
ISREDIT change &BEGA. &ENDB. all nx  9 ' EXEC '  '   EXEC '
ISREDIT change &BEGA. &ENDB. all nx 10 ' EXEC '  '  EXEC '
ISREDIT change &BEGA. &ENDB. all nx 11 '  EXEC '  ' EXEC '
ISREDIT change &BEGA. &ENDB. all nx 11 '   EXEC '  ' EXEC '
ISREDIT change &BEGA. &ENDB. all nx 11 '   EXEC  '  ' EXEC '
ISREDIT BOUNDS
ISREDIT BOUNDS &LBOUND. &RBOUND.
ISREDIT x all    72 ' '
ISREDIT x all    71 ','
ISREDIT locate 0
ISREDIT locate ERROR
IF &LASTCC = 0 THEN DO
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE " "
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE "UNABLE to REALIGN  JCL for +
     lines marked ==ERR> "
   ISREDIT find .ZCSR .ZCSR " " all
END
R: ISREDIT locate ERROR
IF &LASTCC = 0 THEN DO
   ISREDIT find .ZCSR .ZCSR " " all
   GOTO R
END
ISREDIT locate 0
ISREDIT locate CHANGE
IF &LASTCC = 0 THEN DO
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE " "
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE "UNABLE to REALIGN  JCL for +
     lines marked ==ERR> "
   ISREDIT find .ZCSR .ZCSR " " all
END
S: ISREDIT locate CHANGE
IF &LASTCC = 0 THEN DO
   ISREDIT find .ZCSR .ZCSR " " all
   GOTO S
END
ISREDIT locate 0
ISREDIT find nx first 72 p' '
IF &LASTCC = 0 THEN DO
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE " "
   ISREDIT LINE_BEFORE .ZCSR   = NOTELINE "col 72 not blank for one +
     or more lines of JCL      ..55...60...65...70.x"
END
ISREDIT locate 0
