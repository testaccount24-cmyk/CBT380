          /* SYS1.TSOCLIST(BOOKREAD)                              *     /

          PROC 0 HELP
          IF &LIST = LIST THEN CONTROL MAIN MSG LIST CONLIST SYMLIST
                          ELSE CONTROL MAIN NOMSG
          IF HELP = &HELP. THEN DO
             SET TECHX = &SUBSTR(1:4,&SYSPROC.XXXX)
             $BROWSE IBMBK.IS03.BOOKLIST
             IF &LASTCC ^= 0 THEN DO
                CLS
                WRITE ======= HELP FILE MISSING BUT CAN BE RECREATED +
                     =============================
                WRITE THE DATASET IBMBK.IS03.BOOKLIST WHICH CONTAINS +
                      THE NAMES AND DESCRIPTIONS
                WRITE OF ALL BOOKREAD FILES IS MISSING.  IT CAN BE +
                      RECREATED WITH THE CLIST
                WRITE FOUND IN IS03.LIBR.CLIST(BK000).
                WRITE
                WRITE ADDITIONAL DOCUMENTATION CAN BE FOUND +
                      IN IS03.SHARE.TEXT(BOOKR001)
                WRITE ===============================================+
                     =============================
             END
             EXIT CODE(1)
          END
          WRITE FOR LIST A OF BOOKS USE COMMAND ===> TSO BOOKREAD HELP
          WRITE  &SYSLC(WAIT FOR THREE ASTERISKS TO APPEAR AND THEN HIT ENTER)

          SET &EXECFLAG = OFF
          SET &STEP = 999
          SET &RC   = &STEP
          IF &SYSISPF. ^= ACTIVE THEN GOTO ERROR

          SET &ISPPLIB  = SYS1.SEOYPENU                /* PANEL LIBRARY */
          SET &ISPMLIB  = SYS1.SEOYMENU                /* MSG   LIBRARY */
          SET &ISPTLIB  = SYS1.SEOYTENU                /* TLIB  LIBRARY */
          SET &EXEC     = SYS1.SEOYCLIB                /* EXEC  LIBRARY */

          SET &STEP = 100
          EXECUTIL SEARCHDD(YES)
          ALTLIB ACTIVATE APPLICATION(EXEC) DATASET('&EXEC')
          SET &RC=&LASTCC
          IF &RC^=0 THEN GOTO ERROR
                    ELSE SET &EXECFLAG = ON

          SET &STEP = 110
          ISPEXEC CONTROL ERRORS RETURN
          ISPEXEC LIBDEF ISPPLIB DATASET ID('&ISPPLIB')
          SET &RC=&LASTCC
          ISPEXEC CONTROL ERRORS CANCEL
          IF &RC^=0 THEN GOTO ERROR

          SET &STEP = 120
          ISPEXEC CONTROL ERRORS RETURN
          ISPEXEC LIBDEF ISPMLIB DATASET ID('&ISPMLIB')
          SET &RC=&LASTCC
          ISPEXEC CONTROL ERRORS CANCEL
          IF &RC^=0 THEN GOTO ERROR

          SET &STEP = 140
          ISPEXEC CONTROL ERRORS RETURN
          ISPEXEC LIBDEF ISPTLIB DATASET ID('&ISPTLIB')
          SET &RC=&LASTCC
          ISPEXEC CONTROL ERRORS CANCEL
          IF &RC^=0 THEN GOTO ERROR


          ISPEXEC SELECT CMD(%EOXVSTRT) MODE(FSCR) SUSPEND NEWAPPL(EOXR) NOCHECK

          /* COMMON EXIT  POINT

          EXIT: SET &NOP=NOP

          IF &EXECFLAG = ON +
          THEN DO
            SET &STEP = 160
            ALTLIB DEACTIVATE APPLICATION(EXEC)
            SET &RC=&LASTCC
            IF &RC^=0 THEN GOTO ERROR
          END

          EXIT

          /*
          /* COMMON ERROR POINT
          /*
          ERROR: SET &NOP=NOP
          WRITE *******************************************************
          WRITE ***
          WRITE *** BOOK(&STEP.E): BOOKMANAGER STARTUP ERROR
          WRITE ***                  RC=&RC
          WRITE ***
          WRITE *******************************************************
          IF &STEP = 160 THEN EXIT
          ELSE
            GOTO EXIT
 /*******************************************************************/

/*  --- MODS TO SYS1.SEOYCLIB(EOYBLDBK)                            */
 /*      INCREASED SPACE FOR TEMPFILE IN TEMPALLOC FROM (1,5)       */
 /*        THIS IS THE FILE(...), GENERATED DSNAME TYPICALLY ENDS   */
 /*        WITH SOMETHING LIKE THE FOLLOWING:                       */
 /*         .LIST, .LIST3820, .LIST4250, .LIST4028, .LISTPS         */
 /*      INCREASED SPACE FOR BOOKFILE IN BOOKALLOC FROM (1,5)       */
 /* --------------------------------------------------------------- */
 /* BOOKALLOC = "RECFM(F,B) LRECL(4096) SPACE(8,15) CYLINDERS ",    */
 /*             "RELEASE BLKSIZE(4096) DSORG(PS)"                   */
 /* TEMPALLOC = "NEW CYLINDERS SPACE(8,15) DSORG(PS) DIR(0)",       */
 /*             "RECFM(V,B)",                                       */
 /*             "LRECL(8300) BLKSIZE(8304)"                         */
 /*******************************************************************/
