/*REXX functional  replacement for most of the  ddname clist */
/*  Example -- COMMAND ===> TSO LISTDD                                    */
/*  Example -- COMMAND ===> TSO LISTDD ISPPLIB                            */
/* From an article in Chicago Soft Ltd -- "TSO TIMES" Vol. 2 No. 1        */
/*   by Terry Beadle, President, The Productivity Group                   */
/*   minor modification at     to have ddname as an argument              */
/* Includes a correction by Bruce Christopher and Jeff Todd in  Issue #3  */
Arg ddnameS         /* lines with ddname and ddnameX added 1991/10/21     */
ddnameS = translate(" "ddnameS"          ")
@tcb = Storage(21C,4)                     /* get TCB address from PSAAOLD */
@tiot = Storage(D2X(C2D(@tcb)+12),4)      /* add 12 to get TIOT address   */
@1stdd = D2X(C2D(@tiot)+28)               /* point at 1st DD in TIOT      */
@1stddlen = Storage(D2X(C2D(@tiot)+24),1)  /* get its length              */
@tiot = D2X(C2D(@tiot)+24)                /* point at 1st TIOT ddname     */
@jfcb = Storage(D2X(X2D(@tiot)+12),3)     /* get JFCB address from TIOT   */
Say ' ddname  VOLSER  DATA SET NAME'
Say '------------------------------------------------------------'
Do while C2D(@1stddlen) \= 0              /* loop through entire TIOT     */
 If Storage(@1stdd,1) /= '00'X Then Do
  If Storage(@1stdd,8) /= '' Then ddnameX = Storage(@1stdd,8)       /*   */
  j = index(ddnameS,ddnamex);
  if j/=0 Then ddname=ddnameX
  If ddnameS= '' | ddname = ddnameX Then                            /*   */
  Say Storage(@1stdd,8),                  /* display ddname               */
      Storage(D2X(C2D(@jfcb)+134),6),     /* display VOLSER               */
      Storage(D2X(C2D(@jfcb)+16),44)      /* display DSNAME               */
 End
  @tiot = D2X(X2D(@tiot)+C2D(@1stddlen))  /* point to next TIOT entry     */
  @1stddlen = Storage(@tiot,1)            /* get length of tiot entry     */
  @1stdd = D2X(X2D(@tiot)+4)              /* point to ddname in TIOT      */
  @jfcb = Storage(D2X(X2D(@tiot)+12),3)   /* get next JFCB address        */
End
/* LIBRARY CONCATENATIONS ------------------------------------------------*/
/*   SYSPROC:   CLIST LIBRARIES                                           */
/* LIBRARY CONCATENATIONS USED BY ISPF -----------------------------------*/
/*   ISPPLIB:   PANEL DEFINITIONS                                         */
/*   ISPMLIB:   MESSAGE DEFINITIONS  NEW MESSAGES HAVE ISP PREFIX         */
/*   ISPSLIB:   SKELETON DEFINITIONS.   EDIT MODELS (NAMED ISRE....)      */
/*              AND FILE SKELETONS USED BY BATCH PROCESSING (OPTION 5)    */
/*   ISPTLIB:   TABLE INPUT LIBRARY                                       */
/*   ISPPROF:   STORED PROFILE FOR EACH USER, INital profiles             */
/*              ORIGINATE FROM ISPTLIB.                                   */
