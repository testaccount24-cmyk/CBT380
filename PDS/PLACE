ISREDIT MACRO NOPROCESS (MEMBER)
  /* TITLE -- PLACE
  /* DESCRIPTION -- USED TO BUILD up text with modified panels
  /*
  /* except for the next 2 lines-- ripped out of PASTE macro */
  SET KEEP = KEEP
  SET &DATASET = &STR(IS03.PLR.PATTERNS(&MEMBER.))

  IF &SYSDSN('&DATASET') NE &STR(OK) THEN +
     DO
        ISREDIT PROCESS DEST
        IF &LASTCC. ^= 8 THEN DO
          WRITE SORRY -- &DATASET
          WRITE &B.        &SYSDSN('&DATASET')
          EXIT CODE(12)
        END
        ISREDIT (DEST) = LINENUM .ZDEST
        SET DEST = &DEST

/*********************************************/
/* PERFORM RESET OPERATION TO GET RID OF     */
/* THE DESTINATION CHARACTER 'A'.            */
/*********************************************/
        ISREDIT RESET &DEST &DEST
        SET &ZEDSMSG = &STR(WORK DATASET NOT FOUND)
        SET &ZEDLMSG = &STR(PASTE WORK DATASET COULD NOT BE +
                            FOUND)
        ISPEXEC SETMSG MSG(ISRZ001)
        EXIT CODE(12)
     END

  ALLOC F(TEMPFLE) DA('&DATASET') SHR REUSE

/***********************************************/
/* TELL MACRO THAN WE WILL PROCESS B (BEFORE)  */
/*    OR A (AFTER) DESTINATION COMMANDS.       */
/* ONLY DEST IS VALID ANYTHING ELSE IS NOT.    */
/* UNEXPECTED  C RANGE CAUSED ABEND S-0C4.     */
/***********************************************/

  ISREDIT PROCESS DEST RANGE C M

  SET RC = &STR(&LASTCC)
  ISREDIT (CMD) = RANGE_CMD
  IF &RC = 0 OR &RC = 8 THEN DO
     ISREDIT (CMD) = RANGE_CMD
     SET &ZEDSMSG = &STR(RANGE NOT ALLOWED RC=&RC.)
     SET &ZEDLMSG = &STR(RANGE "&CMD." NOT USED WITH PASTE -- +
         USE DEST "A" OR "B" ONLY)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(12)   /* HIGH RETURN CODE LEAVES COMMAND */
   END
  IF &RC NE 4 THEN DO
     SET &ZEDSMSG = &STR(PASTE PENDING)
     SET &ZEDLMSG = &STR(AN "A" OR "B" LINE COMMAND HAS NOT BEEN +
          SPECIFIED, OR LINE COMMANDS CONFLICT)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(12)   /* HIGH RETURN CODE LEAVES COMMAND */
   END

/*********************************************/
/* SET OUR DEST VARIABLE TO THE LINE NUMBER  */
/* FOR A 'A' COMMAND ENTERED BY THE USER.    */
/*********************************************/

  ISREDIT (DEST) = LINENUM .ZDEST

/*********************************************/
/* AFTER THE ISREDIT LINENUM COMMAND, IT IS  */
/* NECESSARY TO SET THE DEST VARIABLE TO     */
/* ITSELF TO DROP ANY LEADING ZEROES IN IT.  */
/*********************************************/

  SET DEST = &DEST
  SET SDEST = &DEST

/*********************************************/
/* FIND OUT IF SEQUENCE NUMBERING ON         */
/*********************************************/
ISREDIT (NUMON) = NUMBER

/*********************************************/
/* SET TSO/E ERROR ROUTINE BEFORE USE OF     */
/* CLIST QSAM ROUTINES TO READ WORK DATASET. */
/*********************************************/

  ERROR +
     DO
        SET RCLINE = &LASTCC.
        IF &RCLINE. = 4 THEN DO
           SET TRUNCAT = &STR( -DATA TRUNCATED- )
           RETURN
        END
        IF &RCLINE NE 400  THEN SET RCNOTE = &STR(** RC=&RCLINE.** )
        ISREDIT LINE_AFTER &DEST. = +
           NOTELINE "&SYSTIME. -END- OF PASTE &KEEP. &TRUNCAT.&RCNOTE."
        ISREDIT LINE_AFTER  &DEST. = +
           DATALINE  ".panel off;.*=====&MEMBER.==============+
                      ====================================="
        SET SDEST = &SDEST. + 1
        ISREDIT LINE_BEFORE &SDEST. = +
           DATALINE  ".panel &MEMBER.;.*======================+
                      ====================================="
        IF &NUMON = ON THEN ISREDIT LINE_AFTER &DEST. = NOTELINE +
           "&SYSTIME. NUMBER MODE IN EFFECT, TRUNCATION LOSS MAY +
           OR MAY NOT BE REAL"
        CLOSFILE TEMPFLE
        FREE F(TEMPFLE)
  /*--REMOVE  CONTROL NOMSG
  /*--REMOVE        IF KEEP NE &KEEP. THEN DELETE '&DATASET'
  /*--REMOVE        CONTROL MSG
        SET &ZEDSMSG = &STR(PASTE SUCCESSFULL)
        SET &ZEDLMSG = &STR(PASTE WORK DATASET WAS MOVED INTO +
                            INTO THIS CURRENT DATASET)
        ISPEXEC SETMSG MSG(ISRZ000)
        EXIT CODE(1)
     END

/*********************************************/
/* USE CLIST QSAM ROUTINES TO WRITE OUT EACH */
/* RECORD BACK TO THE CURRENT EDITED DATASET.*/
/*********************************************/

  OPENFILE TEMPFLE INPUT

  LOOP: GETFILE TEMPFLE
        SET XX = &LASTCC.
        IF &XX NE 0 THEN DO
           WRITE LC=&LASTCC.
           EXIT CODE(12)
        END
        /* NEXT LINE  CORRECTED  D.MCRITCHIE 1991/04/24 */
        ISREDIT LINE_AFTER &DEST = (TEMPFLE)
        IF &SDEST. = &DEST THEN ISREDIT LINE_AFTER &DEST. = +
           NOTELINE "&SYSTIME. START OF PASTE &KEEP."
        SET &DEST = &DEST + 1
        GOTO LOOP

